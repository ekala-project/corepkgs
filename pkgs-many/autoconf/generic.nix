{
  version,
  src-hash,
  patches ? [ ],
  packageAtLeast,
  packageOlder,
  mkVariantPassthru,
  ...
}@variantArgs:

{
  lib,
  stdenv,
  fetchurl,
  m4,
  perl,
  texinfo,
  callPackage,
}:

# Note: this package is used for bootstrapping fetchurl, and thus
# cannot use fetchpatch! All mutable patches (generated by GitHub or
# cgit) that are needed here should be included directly in Nixpkgs as
# files.

stdenv.mkDerivation (finalAttrs: {
  pname = "autoconf";
  inherit version;

  outputs =
    if packageAtLeast "2.71" then
      [
        "out"
        "doc"
      ]
    else
      [ "out" ];

  src = fetchurl {
    url = "mirror://gnu/autoconf/autoconf-${version}.tar.xz";
    hash = src-hash;
  };

  inherit patches;

  strictDeps = packageAtLeast "2.71";

  nativeBuildInputs = [
    m4
    perl
  ]
  ++ lib.optionals (packageAtLeast "2.71") [ texinfo ];

  postBuild = lib.optionalString (packageAtLeast "2.71") ''
    make html
  '';

  postInstall = lib.optionalString (packageAtLeast "2.71") ''
    make install-html
  '';

  # Work around a known issue in Cygwin.  See
  # http://thread.gmane.org/gmane.comp.sysutils.autoconf.bugs/6822 for
  # details.
  # There are many test failures on `i386-pc-solaris2.11'.
  doCheck =
    if packageOlder "2.71" then
      false
    else
      ((!stdenv.hostPlatform.isCygwin) && (!stdenv.hostPlatform.isSunOS));

  doInstallCheck = packageAtLeast "2.71";

  # Don't fixup "#! /bin/sh" in Autoconf, otherwise it will use the
  # "fixed" path in generated files!
  dontPatchShebangs = true;

  enableParallelBuilding = true;

  # Make the Autotest test suite run in parallel.
  preCheck = ''
    export TESTSUITEFLAGS="-j$NIX_BUILD_CORES"
  '';

  meta = {
    homepage = "https://www.gnu.org/software/autoconf/";
    description = "Part of the GNU Build System";

    longDescription = ''
      GNU Autoconf is an extensible package of M4 macros that produce
      shell scripts to automatically configure software source code
      packages.  These scripts can adapt the packages to many kinds of
      UNIX-like systems without manual user intervention.  Autoconf
      creates a configuration script for a package from a template
      file that lists the operating system features that the package
      can use, in the form of M4 macro calls.
    '';

    license = if packageOlder "2.71" then lib.licenses.gpl2Plus else lib.licenses.gpl3Plus;

    platforms = lib.platforms.all;
  };

  passthru = mkVariantPassthru variantArgs // {
    autoreconfHook = callPackage ../../pkgs/autoreconfHook {
      autoconf = finalAttrs.finalPackage;
    };
  };
})
