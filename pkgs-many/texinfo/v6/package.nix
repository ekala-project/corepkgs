{
  version,
  hash,
  isInteractive ? false,
  mkVariantPassthru,
  ...
}@variantArgs:

{
  lib,
  stdenv,
  buildPackages,
  fetchurl,
  perl,
  libintl,
  bashNonInteractive,
  updateAutotoolsGnuConfigScriptsHook,
  gawk,
  freebsd,
  glibcLocales,
  libiconv,
  ncurses,
  procps,
}:

# Note: this package is used for bootstrapping fetchurl, and thus
# cannot use fetchpatch! All mutable patches (generated by GitHub or
# cgit) that are needed here should be included directly in Nixpkgs as
# files.

let
  inherit (lib)
    getBin
    getDev
    getLib
    optional
    optionals
    optionalString
    versionOlder
    ;
  crossBuildTools = stdenv.hostPlatform != stdenv.buildPlatform;
in

stdenv.mkDerivation {
  pname = "texinfo${optionalString isInteractive "-interactive"}";
  inherit version;

  src = fetchurl {
    url = "mirror://gnu/texinfo/texinfo-${version}.tar.xz";
    inherit hash;
  };

  patches = [
    ./fix-glibc-2.34.patch
  ]
  ++ optional crossBuildTools ./cross-tools-flags.patch;

  postPatch = ''
    patchShebangs tp/maintain/regenerate_commands_perl_info.pl
  '';

  env = {
    XFAIL_TESTS = toString (
      optionals stdenv.hostPlatform.isMusl [
        # musl does not support locales.
        "different_languages_gen_master_menu.sh"
        "test_scripts/formatting_documentlanguage_cmdline.sh"
        "test_scripts/layout_formatting_fr_info.sh"
        "test_scripts/layout_formatting_fr.sh"
        "test_scripts/layout_formatting_fr_icons.sh"
      ]
      ++ optionals (!stdenv.hostPlatform.isMusl && versionOlder version "7") [
        # Test is known to fail on various locales on texinfo-6.8:
        #   https://lists.gnu.org/r/bug-texinfo/2021-07/msg00012.html
        "test_scripts/layout_formatting_fr_icons.sh"
      ]
    );
  }
  // lib.optionalAttrs crossBuildTools {
    # ncurses is required to build `makedoc'
    # this feature is introduced by the ./cross-tools-flags.patch
    NATIVE_TOOLS_CFLAGS = "-I${getDev buildPackages.ncurses}/include";
    NATIVE_TOOLS_LDFLAGS = "-L${getLib buildPackages.ncurses}/lib";
  };

  strictDeps = true;
  enableParallelBuilding = true;

  # A native compiler is needed to build tools needed at build time
  depsBuildBuild = [
    buildPackages.stdenv.cc
    perl
  ];

  nativeBuildInputs = [ updateAutotoolsGnuConfigScriptsHook ];
  buildInputs = [
    bashNonInteractive
    libintl
  ]
  ++ optionals stdenv.hostPlatform.isSunOS [
    libiconv
    gawk
  ]
  ++ optional isInteractive ncurses;

  configureFlags = [
    "PERL=${buildPackages.perl}/bin/perl"
  ]
  # Perl XS modules are difficult to cross-compile and texinfo has pure Perl
  # fallbacks.
  # Also prevent the buildPlatform's awk being used in the texindex script
  ++ optionals crossBuildTools [
    "--enable-perl-xs=no"
    "TI_AWK=${getBin gawk}/bin/awk"
  ]
  ++ optionals (crossBuildTools && lib.versionAtLeast version "7.1") [
    "texinfo_cv_sys_iconv_converts_euc_cn=yes"
  ]
  ++ optional stdenv.hostPlatform.isSunOS "AWK=${gawk}/bin/awk";

  installFlags = [ "TEXMF=$(out)/texmf-dist" ];
  installTargets = [
    "install"
    "install-tex"
  ];

  nativeCheckInputs = [ procps ] ++ optionals stdenv.buildPlatform.isFreeBSD [ freebsd.locale ];
  checkInputs = optionals (lib.versionAtLeast version "7.2") [ glibcLocales ];

  doCheck = isInteractive && !stdenv.hostPlatform.isDarwin && !stdenv.hostPlatform.isSunOS; # flaky

  postFixup = optionalString crossBuildTools ''
    for f in "$out"/bin/{pod2texi,texi2any}; do
      substituteInPlace "$f" \
        --replace-fail ${buildPackages.perl}/bin/perl ${perl}/bin/perl
    done
  '';

  passthru = mkVariantPassthru variantArgs;

  meta = {
    homepage = "https://www.gnu.org/software/texinfo/";
    description = "GNU documentation system";
    changelog = "https://git.savannah.gnu.org/cgit/texinfo.git/plain/NEWS";
    license = lib.licenses.gpl3Plus;
    platforms = lib.platforms.all;
    maintainers = [ ];
    branch = version;

    longDescription = ''
      Texinfo is the official documentation format of the GNU project.
      It was invented by Richard Stallman and Bob Chassell many years
      ago, loosely based on Brian Reid's Scribe and other formatting
      languages of the time.  It is used by many non-GNU projects as
      well.

      Texinfo uses a single source file to produce output in a number
      of formats, both online and printed (dvi, html, info, pdf, xml,
      etc.).  This means that instead of writing different documents
      for online information and another for a printed manual, you
      need write only one document.  And when the work is revised, you
      need revise only that one document.  The Texinfo system is
      well-integrated with GNU Emacs.
    '';
    mainProgram = "texi2any";
  };
}
